# check_pattern.awk
# Generated by AX_AT_DATA_CHECK_PATTERN_AWK()
# from https://github.com/lukem/ax_at_check_pattern

BEGIN { exitval=0 }

function mismatch()
{
	print mode
	for (i = 0; i < lc; i++) {
		print ll[i]
	}
	print "---"
	for (i = 0; i < rc; i++) {
		print rl[i]
	}
	mode=""
	exitval=1
}

$1 ~ /^[0-9]+(,[0-9]+)?[ad][0-9]+(,[0-9]+)?$/ {
	print
	mode=""
	exitval=1
	next
}

$1 ~ /^[0-9]+(,[0-9]+)?[c][0-9]+(,[0-9]+)?$/ {
	mode=$1
	lc=0
	rc=0
	next
}

mode == "" {
	print $0
	next
}

$1 == "<" {
	ll[lc] = $0
	lc = lc + 1
	next
}

$1 == "---" {
	next
}

$1 == ">" {
	rl[rc] = $0
	rc = rc + 1
	if (rc > lc) {
		mismatch()
		next
	}
	pat = "^" substr(ll[rc-1], 2) "$"
	str = substr($0, 2)
	if (str !~ pat) {
		mismatch()
	}
	next
}
{
	print "UNEXPECTED LINE: " $0
	exit 10
}

END { exit exitval }
