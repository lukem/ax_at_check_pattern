AT_SETUP([diff function])

AT_DATA([1.pattern],
[first line is ok
second line changes .*
second( line)? changes .*
third line
fourth line
fifth line
sixth line
])

AT_DATA([1.output],
[new first line
first line is ok
second line changes Sat Nov  9 08:23:01 EST 2013
second changes *
third line
fourth line change
fourth.2 new
fifth line
])

dnl normal diff output
dnl
AT_CHECK([diff 1.pattern 1.output], [1],
[0a1
> new first line
2,3c3,4
< second line changes .*
< second( line)? changes .*
---
> second line changes Sat Nov  9 08:23:01 EST 2013
> second changes *
5c6,7
< fourth line
---
> fourth line change
> fourth.2 new
7d8
< sixth line
])

dnl ax_at_diff_pattern output; diff with the 'second' regexes elided
dnl
AT_CHECK([ax_at_diff_pattern 1.pattern 1.output], [1],
[0a1
> new first line
5c6,7
< fourth line
---
> fourth line change
> fourth.2 new
7d8
< sixth line
])

AT_CLEANUP()
